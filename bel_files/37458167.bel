##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Name = "37458167.bel"
SET DOCUMENT Authors = "Sutrishna Sen"
SET DOCUMENT ContactInfo = "sutrishna.sen@causalitybiomodels.com"
SET DOCUMENT Description = "COMMUTE-WP4-BEL_curation"

SET DOCUMENT Licenses = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT Copyright = "Copyright © 2024 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Version = "2.0"


##################################################################################
# NAMESPACES Section
##################################################################################

DEFINE NAMESPACE ADO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/alzheimer-disease-ontology/alzheimer-disease-ontology-1.0.2.belns"
#DEFINE NAMESPACE BRCO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/brain-region-ontology/brain-region-ontology-1.0.0.belns"
DEFINE NAMESPACE NIFT AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/nift/NIFT.belns"

DEFINE NAMESPACE HGNC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc/hgnc-20190708.belns"
DEFINE NAMESPACE MGI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20190128.belns"
DEFINE NAMESPACE RGD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20190128.belns"
DEFINE NAMESPACE GFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc-gene-families/hgnc-genefamily-names-20181221.belns"
DEFINE NAMESPACE SCOMP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20190128.belns"
DEFINE NAMESPACE SFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20190128.belns"
DEFINE NAMESPACE HP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20190130.belns"
DEFINE NAMESPACE CHEBI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/chebi/chebi-20190708.belns"
DEFINE NAMESPACE DRUGBANK AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/drugbank/drugbank-20180906.belns"
DEFINE NAMESPACE INTERPRO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/interpro/interpro-names-20181021.belns"
DEFINE NAMESPACE PFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/pfam/pfam-names-20181024.belns"
DEFINE NAMESPACE DO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20200407.belns"
DEFINE NAMESPACE GO AS URL 	"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go/go-20180109.belns"
DEFINE NAMESPACE GOBP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-biological-process/go-biological-process-20190128.belns"
DEFINE NAMESPACE GOCC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-cellular-component/go-cellular-component-20190128.belns"

DEFINE NAMESPACE MESH AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh/mesh-names-20181007.belns"

DEFINE NAMESPACE MESHC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-chemicals/mesh-chemicals-20190128.belns"
DEFINE NAMESPACE MESHD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-diseases/mesh-diseases-20190128.belns"
DEFINE NAMESPACE MESHPP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-processes/mesh-processes-20190128.belns"
DEFINE NAMESPACE MESHA AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-anatomy/mesh-anatomy-20190128.belns"
DEFINE NAMESPACE MESHCS AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-cellular-structures/mesh-cellular-structures-20190128.belns"
DEFINE NAMESPACE NCBIGENE AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-gene/ncbi-gene-20190224.belns"
DEFINE NAMESPACE NCIT AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncit/ncit-20200603.belns"
DEFINE NAMESPACE PATHWAY AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/pw/pw-20171007.belns"
DEFINE NAMESPACE COVID AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-covid19/ncbi-covid19-20200326.belns"
DEFINE NAMESPACE EFO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/efo/efo-20171206.belns"
DEFINE NAMESPACE CMO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/cmo/cmo-20171030.belns"

DEFINE NAMESPACE UNIPROT AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM AS PATTERN "^\d+$"
DEFINE NAMESPACE dbSNP AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE FIXMECHEM AS LIST {".."}
DEFINE NAMESPACE TAX AS PATTERN "^.*$"
DEFINE NAMESPACE FIXME AS LIST {"long COVID", "common respiratory viruses", "post-acute infection syndrome", "neuronal injury", "neural circuit dynamics","Chronic Cough"}
##################################################################################
# ANNOTATIONS Section
##################################################################################
DEFINE ANNOTATION Anatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20190128.belanno"
DEFINE ANNOTATION CellLine AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20190128.belanno"
DEFINE ANNOTATION Cell AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20200623.belanno"
DEFINE ANNOTATION Disease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20200622.belanno"
DEFINE ANNOTATION MeSHAnatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20190128.belanno"
DEFINE ANNOTATION MeSHDisease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20190128.belanno"
DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"
DEFINE ANNOTATION TextLocation AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"
DEFINE ANNOTATION BioAssay AS URL   "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/bao/bao-20190130.belanno"

DEFINE ANNOTATION CovidAccession AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/ncbi-covid19/ncbi-accession-covid19-20200326.belanno"

# Document Annotations #
DEFINE ANNOTATION PublicationType AS LIST {"Review", "Research"}
DEFINE ANNOTATION PublicationStatus AS LIST {"Preprint", "Published"}
DEFINE ANNOTATION Section AS LIST {"Full Text", "Abstract", "Results"}

DEFINE ANNOTATION Timeline AS LIST {"6 months after acute infection", "6 months after acute COVID"}
DEFINE ANNOTATION Age AS LIST {"70 years and above", "older than 65 years", "aging population"}
DEFINE ANNOTATION Custom_anatomy AS LIST {"Subcortical region"}
DEFINE ANNOTATION Custom_Disease AS LIST {"Long COVID", "developed encephalopathy during acute infection", "short-term SARS-CoV-2 infection"}
DEFINE ANNOTATION Disease_Severity AS LIST {"severe"}
DEFINE ANNOTATION Disease_Condition AS LIST {"acute"}

##################################################################################
# Statements #
##################################################################################

SET Citation = {"PubMed", "Dis Model Mech. 2023 Jul 1;16(7):dmm050049. doi: 10.1242/dmm.050049.", "37458167"}


SET PublicationType = "Review"
SET PublicationStatus = "Published"
SET Section = "Full Text"


SET Species = "9606"

SET Support = "SARS-CoV-2 infection occurs when the virus spike (S) protein binds to the zinc metalloproteinase angiotensin-converting enzyme 2 (ACE2) at the host cell membrane (Hoffmann et al., 2020a; Lan et al., 2020; Shang et al., 2020a; Wan et al., 2020) (Fig. 2)."
SET MeSHAnatomy = "Cell Membrane"
complex(p(UNIPROT:P0DTC2), p(HGNC:ACE2)) -> path(DO:"COVID-19")
UNSET MeSHAnatomy

SET Support = "Alternatively, in the non-endosomal pathway, the primary activation is based on the cleavage of S protein by the cellular transmembrane serine protease 2 (TMPRSS2) (Koch et al., 2021) and subsequent spike cleavage by the endonuclease furin (Bestle et al., 2020; Shang et al., 2020b)."
p(HGNC:TMPRSS2) -> p(UNIPROT:P0DTC2, frag("?"))
p(HGNC:FURIN) -> p(UNIPROT:P0DTC2, frag("?"))

SET Support = "Additionally, in cells with low ACE2 expression, viral infection can be enhanced by additional host factors, such as the extracellular protease BSG (also known as CD147), the transmembrane receptor NRP1 (Cantuti-Castelvetri et al., 2020), lectins (Lempp et al., 2021), or HAVCR1 phosphatidylserine receptors such TIM-1 (also known as TIMELESS) and AXL (Bohan et al., 2021; Wang et al., 2021b)."
p(HGNC:BSG) -> path(DO:"COVID-19")
r(HGNC:NRP1) -> path(DO:"COVID-19")
a(MESH:Lectins) -> path(DO:"COVID-19")
p(HGNC:HAVCR1) -> path(DO:"COVID-19")
p(HGNC:TIMELESS) -> path(DO:"COVID-19")
p(HGNC:AXL ) -> path(DO:"COVID-19")

SET Support = "Studies based on publicly available transcriptome databases revealed that one of the most likely enhancers of direct viral infection in the brain is NRP1, which is highly expressed in multiple neuron and glial cell types, including astrocytes and microglia (Cantuti-Castelvetri et al., 2020; Daly et al., 2020; Lonsdale et al., 2013)."
SET MeSHAnatomy = {"Brain","Neurons", "Astrocytes", "Microglia"}
p(HGNC:NRP1) -> path(DO:"COVID-19")
UNSET MeSHAnatomy

SET Support = "Recently, the extracellular proteases BSG and DPP4 have also been identified as additional entry receptors for SARS-CoV-2 on astrocytes, providing new detail on the neuropathogenesis of COVID-19 (Andrews et al., 2022; Wang et al., 2020) (Fig. 2)."
SET MeSHAnatomy = "Astrocytes"
p(HGNC:BSG) -> bp(GO:"viral entry into host cell")
p(HGNC:DPP4) -> bp(GO:"viral entry into host cell")
UNSET MeSHAnatomy

SET Support = "Postmortem analyses of COVID-19 patients' brain tissue revealed T-cell infiltration and microglial activation, and injured neurons and astrocytes (Kanberg et al., 2020; Schwabenland et al., 2021)."
SET MeSHAnatomy = "Brain"
path(DO:"COVID-19") -> a(MESH:"T-Lymphocytes", loc(MESH:Brain))
path(DO:"COVID-19") -> bp(GO:"microglial cell activation")
UNSET MeSHAnatomy
SET MeSHAnatomy = {"Brain","Neurons", "Astrocytes"}
path(DO:"COVID-19") -> a(NCIT:Injury)
UNSET MeSHAnatomy

SET Support = "Despite the difficulties in adequate diagnosis, long COVID includes a combination of multifactorial symptoms such as fatigue, dyspnea, persistent cough, chest pain, persistent headache, memory and cognitive impairment, muscle aches, loss of smell or taste, depression and/or anxiety (Davis et al., 2023; de Erausquin et al., 2021; Lechien et al., 2020; Vos et al., 2022)."
path(FIXME:"long COVID") -> path(MESH:Fatigue)
path(FIXME:"long COVID") -> path(MESH:Dyspnea)
path(FIXME:"long COVID") -> path(FIXME:"Chronic Cough")
path(FIXME:"long COVID") -> path(MESH:"Chest Pain")
path(FIXME:"long COVID") -> path(MESH:"Headache Disorders")
path(FIXME:"long COVID") -> path(HP:"Memory impairment")
path(FIXME:"long COVID") -> path(HP:"Cognitive impairment")
path(FIXME:"long COVID") -> path(MESH:Myalgia)
path(FIXME:"long COVID") -> path(HP:Anosmia)
path(FIXME:"long COVID") -> path(MESH:Ageusia)
path(FIXME:"long COVID") -> path(DO:"mental depression") 
path(FIXME:"long COVID") -> path(HP:Anxiety)

SET Support = "Some long COVID patients report persistent symptoms 6 months after acute infection, most commonly dyspnea and a diverse array of neurological problems (Box 1, Fig. 1)."
SET Timeline = "6 months after acute infection"
path(FIXME:"long COVID") -> path(MESH:Dyspnea)
path(FIXME:"long COVID") -> path(HP:"Abnormality of nervous system physiology")



SET Support = "For example, a meta-analysis of ~10,000 COVID-19 studies revealed the persistence of headache in between 8% and 15% of patients in the first 6 months after SARS-CoV-2 infection (Fernández-de-las-Peñas et al., 2021), and two independent patient cohort studies reported partial taste and smell disorders in 40% of patients (Lechien et al., 2020; Leedman et al., 2021)."
path(FIXME:"long COVID") -> path(MESH:Headache)
path(FIXME:"long COVID") -> a(NCIT:Hypogeusia)
path(FIXME:"long COVID") -> path(HP:Anosmia)
UNSET Timeline

SET Support = "A similar meta-analysis also revealed that patients of 70 years and above are at higher risk of long COVID, although it is not exclusive to adults (Sudre et al., 2021)."
SET Age = "70 years and above"
path(FIXME:"long COVID") -- bp(GO:aging)
UNSET Age

SET Support = "Along with female sex and older age, low socioeconomic status, smoking, obesity and a large panel of other comorbidities, i.e. chronic obstructive pulmonary disease, prostatic hyperplasia, fibromyalgia, depression or multiple sclerosis, among others, have also been associated with a higher long-term risk of long COVID (Subramanian et al., 2022; Thompson et al., 2022)."
a(NCIT:Age) pos path(FIXME:"long COVID")
a(NCIT:"Socioeconomic Factors") pos path(FIXME:"long COVID")
a(NCIT:Smoking) pos path(FIXME:"long COVID")
path(DO:"chronic obstructive pulmonary disease") pos path(FIXME:"long COVID")
path(MESH:"Prostatic Hyperplasia") pos path(FIXME:"long COVID")
path(DO:fibromyalgia) pos path(FIXME:"long COVID")
path(DO:"mental depression") pos path(FIXME:"long COVID")
path(DO:"amyotrophic lateral sclerosis") pos path(FIXME:"long COVID")

SET Support = "The most frequently reported manifestations of long COVID are neurological symptoms (hereafter, nLong COVID), which include headache, confusion and ‘brain fog’, mood disorders and dementia(Fig. 3)."
path(FIXME:"long COVID") -> path(MESH:Headache)
path(FIXME:"long COVID") -> path(MESH:Confusion)
path(FIXME:"long COVID") -> path(MESH:"Mental Fatigue")
path(FIXME:"long COVID") -> path(MESH:"Mood Disorders")
path(FIXME:"long COVID") -> path(DO:dementia)


SET Support = "A recent study showed that the cytokine CCL11 (eotaxin-1) remains elevated in long COVID patients suffering from brain fog, and high levels of CCL11 have been associated with demyelination of oligodendrocytes and activation of hippocampal microglia (Fernández-Castañeda et al., 2022)."
SET Custom_Disease = "Long COVID"
path(MESH:"Mental Fatigue") pos p(HGNC:CCL11)
SET Cell = "oligodendrocyte"
p(HGNC:CCL11) -> a(NCIT:Demyelination)
UNSET Cell
SET MeSHAnatomy = "Hippocampus"
p(HGNC:CCL11) -> bp(GO:"microglial cell activation")
UNSET MeSHAnatomy
UNSET Custom_Disease


SET Support = "Beyond nLong COVID itself, there is concern about an increased risk of dementia as a longer-term consequence of COVID-19. A number of viruses, including common respiratory viruses, cause post-acute infection syndromes (Choutka et al., 2022), with a significant number of patients developing long-term neurological sequelae, including neurodegenerative multiple sclerosis (Bjornevik et al., 2022; Cermelli and Jacobson, 2000)."
path(DO:"COVID-19") -> path(DO:dementia)
a(FIXME:"common respiratory viruses") -> path(FIXME:"post-acute infection syndrome")
a(FIXME:"common respiratory viruses") -> path(DO:"multiple sclerosis")


SET Support = "Researchers have observed a significant and progressive reduction in COVID-19 patients’ gray matter in the parahippocampal gyrus, the entorhinal cortex and the hippocampus after SARS-CoV-2 infection (Di Stadio et al., 2022; Douaud et al., 2022; Griffanti et al., 2021). These regions relate to olfaction and memory function."
SET MeSHAnatomy = {"Parahippocampal Gyrus", "Entorhinal Cortex", "Hippocampus"}
path(DO:"COVID-19") -| a(MESH:"Gray Matter")
UNSET MeSHAnatomy
path(DO:"COVID-19") -| bp(GO:"olfactory behavior")
path(DO:"COVID-19") -| bp(GO:"memory")



SET Support = "Accordingly, the risk of being diagnosed with dementia is increased in the 6 months after acute COVID. In the general population, the risk is 0.7% higher, and it rises to 2.7% in patients older than 65 years and to 4.7% in those who have developed encephalopathy during acute infection (Taquet et al., 2021)."
SET Disease_Condition = "acute"
SET Timeline = "6 months after acute COVID"
path(DO:"COVID-19") -> path(DO:dementia)
UNSET Timeline
SET Age = "older than 65 years"
path(DO:"COVID-19") -> path(DO:dementia)
UNSET Age
SET Custom_Disease = "developed encephalopathy during acute infection"
path(DO:"COVID-19") -> path(DO:dementia)
UNSET Custom_Disease


SET Support = "First, the most severely hit population by COVID-19 is the age group of 65 years and above (WHO World Health Statistics, 2023), with an increased risk of poor outcome for those with dementia (Numbers et al., 2021)."
SET Age = "older than 65 years"
SET MeSHDisease = "Dementia"
path(DO:"COVID-19") -> path(MESH:Death)
UNSET MeSHDisease
UNSET Age

SET Support = "Indeed, carrying the Alzheimer’s disease risk allele APOE ɛ4 (Kuo et al., 2020; Wang et al., 2021c) or being of an advanced age (González-García et al., 2021) increase the risk of severe COVID-19."
p(HGNC:APOE, var("?")) -> path(DO:"Alzheimer's disease")
SET Disease_Severity = "severe"
p(HGNC:APOE, var("?")) -> path(DO:"COVID-19")
bp(GO:aging) -- path(DO:"COVID-19")
UNSET Disease_Severity

SET Support = "Conversely, COVID-19 patients with pre-existing neurological syndromes such as Guillain–Barré syndrome exhibit impairment in amyloid processing along with increased biomarkers of neurodegenerative diseases in their cerebrospinal fluid (CSF), including neuroinflammatory cytokines, glial fibrillary acidic protein and neuronal injury markers (Guasp et al., 2022; Jarius et al., 2022; Kanberg et al., 2020; Pilotto et al., 2021; Ziff et al., 2022; Visser et al., 2022 preprint)."
SET Disease = "COVID-19"
path(DO:"Guillain-Barre syndrome") -| bp(GO:"amyloid-beta metabolic process") 
path(DO:"Guillain-Barre syndrome") -> bp(GO:"cytokine production involved in inflammatory response")
path(DO:"Guillain-Barre syndrome") -> p(HGNC:GFAP)
SET MeSHAnatomy = "Neurons"
path(DO:"Guillain-Barre syndrome") -> a(NCIT:Injury)
UNSET MeSHAnatomy

SET Support = "Moreover, an increase in soluble ACE2 levels has been detected in patients with cardiovascular and inflammatory conditions, in those with COVID-19 and in the aging population, indicating convergence to the ACE2–renin–angiotensin system (Batlle et al., 2022; Verdecchia et al., 2020). "
path(DO:"cardiovascular system disease") -> p(HGNC:ACE2)
bp(GO:"inflammatory response") -> p(HGNC:ACE2)


SET Support = "Second, the increased levels of cytokines and chemokines, such as CCL11, upon acute COVID infection are sufficient to induce long-lasting reactivity of white matter microglia in the subcortical and hippocampal regions, compatible with the pathophysiology of cognitive impairment (Etter et al., 2022; Fernández-Castañeda et al., 2022)."
SET MeSHAnatomy = {"Hippocampus", "White Matter"}
SET Custom_anatomy = "Subcortical region"
p(HGNC:CCL11) -> bp(GO:"microglial cell activation")
UNSET Custom_anatomy
UNSET MeSHAnatomy

SET Support = "The reduction in myelinating oligodendrocytes causes loss of myelin and, ultimately, alters cognitive function and neural circuit dynamics (Steadman et al., 2020)."
a(MESH:"Oligodendroglia") -> bp(GO:myelination)
a(NCIT:Demyelination) -| bp(GO:cognition)
a(NCIT:Demyelination) reg bp(FIXME:"neural circuit dynamics")


SET Support = "Yet, common phenotypes emerge upon analysis of short-term SARS-CoV-2 infection in brain organoids, including increased apoptosis, increased oxidative stress in neurons, myelin disregulation and synapsis loss due to microglial activation (Mesci et al., 2022; Samudyata et al., 2022; Song et al., 2021a)."
SET MeSHAnatomy = {"Brain", "Organoids", "Neurons"}
SET Custom_Disease = "short-term SARS-CoV-2 infection"
path(DO:"COVID-19") -> bp(GO:"neuron apoptotic process")
path(DO:"COVID-19") -> bp(MESH:"Oxidative Stress")
path(DO:"COVID-19") -| bp(GO:"regulation of myelination")
path(DO:"COVID-19") -| bp(GO:synapsis)
path(DO:"COVID-19") -> bp(GO:"microglial cell activation")
UNSET Custom_Disease
UNSET MeSHAnatomy

SET Support = "Interestingly, SARS-CoV-2 infection in brain organoids increased the number of reactive astrocytes adjacent to infected ones, increasing neuronal cell death in the vicinity probably due to a local inflammatory reaction (Andrews et al., 2022) (Fig. 4)."
SET MeSHAnatomy = {"Brain", "Organoids"}
path(DO:"COVID-19") -> bp(GO:"neuron apoptotic process")
path(DO:"COVID-19") -> bp(GO:"astrocyte activation")
UNSET MeSHAnatomy

SET Support = "SARS-CoV-2 infection triggers cellular changes in the brain compatible with the progression of neurodegeneration (Heneka et al., 2020; Yang et al., 2021), which was recapitulated in brain organoid models of acute infection."
path(DO:"COVID-19") -> path(HP:Neurodegeneration)
UNSET Disease
UNSET Section
UNSET PublicationStatus
UNSET PublicationType
UNSET Citation