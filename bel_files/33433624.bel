##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Name = "33433624.bel"
SET DOCUMENT Authors = "Abish K"
SET DOCUMENT ContactInfo = "abish@causalitybiomodels.com"
SET DOCUMENT Description = "COMMUTE-WP4-BEL_curation"

SET DOCUMENT Licenses = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT Copyright = "Copyright © 2024 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Version = "2.0"


##################################################################################
# NAMESPACES Section
##################################################################################

DEFINE NAMESPACE ADO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/alzheimer-disease-ontology/alzheimer-disease-ontology-1.0.2.belns"
DEFINE NAMESPACE BRCO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/brain-region-ontology/brain-region-ontology-1.0.0.belns"
DEFINE NAMESPACE CELL AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/cell/cell-20200623.belns"
DEFINE NAMESPACE NIFT AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/nift/NIFT.belns"

DEFINE NAMESPACE HGNC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc/hgnc-20190708.belns"
DEFINE NAMESPACE MGI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20190128.belns"
DEFINE NAMESPACE RGD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20190128.belns"
DEFINE NAMESPACE GFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc-gene-families/hgnc-genefamily-names-20181221.belns"
DEFINE NAMESPACE SCOMP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20190128.belns"
DEFINE NAMESPACE SFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20190128.belns"
DEFINE NAMESPACE HP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20190130.belns"
DEFINE NAMESPACE CHEBI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/chebi/chebi-20190708.belns"
DEFINE NAMESPACE DRUGBANK AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/drugbank/drugbank-20180906.belns"
DEFINE NAMESPACE INTERPRO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/interpro/interpro-names-20181021.belns"
DEFINE NAMESPACE PFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/pfam/pfam-names-20181024.belns"
DEFINE NAMESPACE DO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20200407.belns"
DEFINE NAMESPACE GO AS URL 	"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go/go-20180109.belns"
DEFINE NAMESPACE GOBP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-biological-process/go-biological-process-20190128.belns"
DEFINE NAMESPACE GOCC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-cellular-component/go-cellular-component-20190128.belns"
DEFINE NAMESPACE MESH AS URL 	"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh/mesh-names-20181007.belns"
DEFINE NAMESPACE MESHC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-chemicals/mesh-chemicals-20190128.belns"
DEFINE NAMESPACE MESHD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-diseases/mesh-diseases-20190128.belns"
DEFINE NAMESPACE MESHPP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-processes/mesh-processes-20190128.belns"
DEFINE NAMESPACE MeSHAnatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-anatomy/mesh-anatomy-20190128.belns"
DEFINE NAMESPACE MESHCS AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-cellular-structures/mesh-cellular-structures-20190128.belns"
DEFINE NAMESPACE NCBIGENE AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-gene/ncbi-gene-20190224.belns"
DEFINE NAMESPACE COVID AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-covid19/ncbi-covid19-20200326.belns"
DEFINE NAMESPACE NCIT AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncit/ncit-20200603.belns"
DEFINE NAMESPACE MESHPP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-processes/mesh-processes-20190128.belns"
DEFINE NAMESPACE TAX  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-taxonomy/ncbi-taxonomy-20200322.belns"
#DEFINE NAMESPACE UNIPROT AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
#DEFINE NAMESPACE PUBCHEM AS PATTERN "^\d+$"
#DEFINE NAMESPACE dbSNP AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE FIXMECHEM AS LIST {".."}
DEFINE NAMESPACE FIXME AS LIST {".."}

##################################################################################
# ANNOTATIONS Section
##################################################################################
DEFINE ANNOTATION Anatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20190128.belanno"
DEFINE ANNOTATION CellLine AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20190128.belanno"
DEFINE ANNOTATION Cell AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20190128.belanno"
DEFINE ANNOTATION Disease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20190128.belanno"
DEFINE ANNOTATION MeSHAnatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20190128.belanno"
DEFINE ANNOTATION MeSHDisease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20190128.belanno"
DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"
DEFINE ANNOTATION TextLocation AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"
DEFINE ANNOTATION BioAssay AS URL   "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/bao/bao-20190130.belanno"
DEFINE ANNOTATION CovidAccession AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/ncbi-covid19/ncbi-accession-covid19-20200326.belanno"
DEFINE ANNOTATION Custom_CellLine AS LIST {"induced human neural progenitor cell"}
DEFINE ANNOTATION Technique AS LIST {"TUNEL staining"}

# Document Annotations #
DEFINE ANNOTATION PublicationType AS LIST {"Review", "Research"}
DEFINE ANNOTATION PublicationStatus AS LIST {"Preprint", "Published"}
DEFINE ANNOTATION Section AS LIST {"Full Text", "Abstract", "Results"}

##################################################################################
# Statements #
##################################################################################

SET Citation = {"PubMed", "J Exp Med. 2021 Mar 1; 218(3) doi: 10.1084/jem.20202135", "33433624"}


SET PublicationType = "Research"
SET PublicationStatus = "Published"
SET Section = "Results"

SET Support = "In addition, Tdt-mediated dUTP-biotin nick end labeling (TUNEL) staining indicated increased cell death (Fig. S1 A)."

SET Species = "9606"
SET Custom_CellLine = "induced human neural progenitor cell"
SET Technique = "TUNEL staining"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"cell death")
UNSET Technique

SET Support = "Organoid infection resulted in extensive neuronal cell death; strikingly, however, the majority of TUNEL-positive cells were SARS- CoV-2 negative (Fig. 2, F and G; and Fig. S1 F), and only ~15% of the cells infected with SARS-CoV-2 were TUNEL positive (Fig. 2 H). Increased cell death was correlated with a higher density of SARS- CoV-2–positive cells (Fig. 2 I), which, however, were not overtly dying (Fig. 2, G and H)."

SET MeSHAnatomy = "Organoids"
SET Technique = "TUNEL staining"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"cell death")
UNSET Technique
UNSET MeSHAnatomy

SET Support = "With an added SARS-CoV-2 annotation, SARS-CoV-2 transcript reads were localized to a variety of cell clusters (Fig. 3 A), demonstrating the widespread infectivity of SARS-CoV-2 in neurons, radial glia, and neuronal progenitor cells."

SET MeSHAnatomy = "Organoids"
SET BioAssay = "rna-seq method"
path(DO:"COVID-19") -> a(MESH:"RNA, Viral", loc(MESH:Neurons))
path(DO:"COVID-19") -> a(MESH:"RNA, Viral", loc(MESH:"Ependymoglial Cells"))
path(DO:"COVID-19") -> a(MESH:"RNA, Viral", loc(CELL:"neural progenitor cell"))
a(MESH:"RNA, Viral") -- a(TAX:"Severe acute respiratory syndrome coronavirus 2")
UNSET BioAssay
UNSET MeSHAnatomy

SET Support = "This is consistent with findings from TUNEL staining (Fig. 2, E–G), which demonstrated minimal overlap between SARS-CoV-2–infected cells and those undergoing cell death."

SET MeSHAnatomy = "Organoids"
SET Technique = "TUNEL staining"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"cell death")
UNSET Technique

SET Support = "SARS-CoV-2–infected brain organoid up-regulated pathways related to cell division, organelle fission, and metabolic processes, while ZIKV showed enrichment in type I IFN pathways (Fig. 5 D)."

SET MeSHAnatomy = "Organoids"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"cell division")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"organelle fission")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"cellular metabolic process")
a(TAX:"Zika virus") -> bp(GO:"type I interferon signaling pathway")
UNSET MeSHAnatomy

SET Support = "SARS-CoV-2–positive cells showed enrichment of genes corresponding to viral transcription, along with enrichment for metabolic processes including electron transport–coupled proton transport, cytochrome c to oxygen, and NADH to ubiquinone (Fig. 5 E, top panel)."

SET MeSHAnatomy = "Organoids"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"viral transcription")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"electron transport coupled proton transport")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"mitochondrial electron transport, cytochrome c to oxygen")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"mitochondrial electron transport, NADH to ubiquinone")
UNSET MeSHAnatomy

SET Support = "Finally, we confirmed that infection by SARS-CoV-2 induced a locally hypoxic environment in neuronal regions by staining for HIF1α (Fig. 5, F and G) in mock-infected and SARS-CoV-2–infected organoids."

SET MeSHAnatomy = "Organoids"
SET BioAssay = "immunofluorescent labeling"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"cellular response to hypoxia")
UNSET BioAssay
UNSET MeSHAnatomy

SET Support = "We detected significant inhibition of SARS-CoV-2 infection upon pretreatment with ACE2 antibody compared with isotype control, indicating the requirement of ACE2 for infection of brain organoids (Fig. 6, B and F; and Fig. S2 C)."

SET MeSHAnatomy = "Organoids"
act(p(HGNC:ACE2)) -> bp(GO:"viral entry into host cell")
act(p(HGNC:ACE2)) -> tloc(a(TAX:"Severe acute respiratory syndrome coronavirus 2"), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
UNSET MeSHAnatomy
UNSET Species

SET Support = "In many instances, the expression of the virus coincided with a disruption in the normal vascular topology expected in the cortex, with an important loss of the normal enrichment in radially oriented vessels in upper layers."

SET Species = "10090"
SET MeSHAnatomy = "Cerebral Cortex"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -| bp(GO:"regulation of vascular permeability")
UNSET MeSHAnatomy

SET Support = "However, intraventricular administration of SARS-CoV-2 resulted in weight loss and death, even at the challenge virus dose 100-fold lower than that used for intranasal infection (Fig. 7, I and J)."

a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> path(MESH:"Weight Loss")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> path(MESH:Death)

SET Support = "Upon histological examination, we found multiple microscopic ischemic infarcts in the subcortical white matter, ranging from acute to subacute, and with focal hemorrhagic conversion (Fig. 8 C and Fig. S5 A)."

SET Species = "9606"
SET MeSHAnatomy = "White Matter"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> path(DO:ischemia)
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> path(MESH:Hemorrhage)


SET Support = "Most infarcts showed signs of tissue damage and localized cell death, and positive viral staining was present predominantly around the edges of the infarct and to a lesser degree within the center."

a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> a(NCIT:"Tissue Damage")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> bp(GO:"cell death")
UNSET MeSHAnatomy
UNSET Species
UNSET PublicationType
UNSET PublicationStatus
UNSET Section
UNSET Citation