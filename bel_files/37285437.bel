##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Name = "37285437.bel"
SET DOCUMENT Authors = "Febin Seethi V"
SET DOCUMENT ContactInfo = "febin.seethi@causalitybiomodels"
SET DOCUMENT Description = "COMMUTE-WP4-BEL_curation"

SET DOCUMENT Licenses = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT Copyright = "Copyright © 2020 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Version = "2.0"

##################################################################################
# NAMESPACES Section
##################################################################################

DEFINE NAMESPACE ADO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/alzheimer-disease-ontology/alzheimer-disease-ontology-1.0.2.belns"
DEFINE NAMESPACE BRCO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/brain-region-ontology/brain-region-ontology-1.0.0.belns"
DEFINE NAMESPACE CELL AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/cell/cell-20200623.belns"
DEFINE NAMESPACE NIFT AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/nift/NIFT.belns"
DEFINE NAMESPACE CMO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/cmo/cmo-20171030.belns"
DEFINE NAMESPACE PW AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/pw/pw-20171007.belns"
DEFINE NAMESPACE NCIT AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncit/ncit-20200603.belns"
DEFINE NAMESPACE HGNC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc/hgnc-20190708.belns"
DEFINE NAMESPACE MGI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20190128.belns"
DEFINE NAMESPACE RGD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20190128.belns"
DEFINE NAMESPACE GFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc-gene-families/hgnc-genefamily-names-20181221.belns"
DEFINE NAMESPACE SCOMP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20190128.belns"
DEFINE NAMESPACE SFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20190128.belns"
DEFINE NAMESPACE HP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20190130.belns"
DEFINE NAMESPACE CHEBI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/chebi/chebi-20190708.belns"
DEFINE NAMESPACE DRUGBANK AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/drugbank/drugbank-20180906.belns"
DEFINE NAMESPACE INTERPRO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/interpro/interpro-names-20181021.belns"
DEFINE NAMESPACE PFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/pfam/pfam-names-20181024.belns"
DEFINE NAMESPACE DO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20200407.belns"
DEFINE NAMESPACE GO AS URL 	"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go/go-20180109.belns"
DEFINE NAMESPACE GOBP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-biological-process/go-biological-process-20190128.belns"
DEFINE NAMESPACE GOCC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-cellular-component/go-cellular-component-20190128.belns"
DEFINE NAMESPACE MESHC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-chemicals/mesh-chemicals-20190128.belns"
DEFINE NAMESPACE MESH AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh/mesh-names-20181007.belns"
DEFINE NAMESPACE MESHD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-diseases/mesh-diseases-20190128.belns"
DEFINE NAMESPACE MESHPP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-processes/mesh-processes-20190128.belns"
DEFINE NAMESPACE MESHA AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-anatomy/mesh-anatomy-20190128.belns"
DEFINE NAMESPACE MESHCS AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-cellular-structures/mesh-cellular-structures-20190128.belns"
DEFINE NAMESPACE MESHF AS URL"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-psychology/mesh-psychology-20170419.belns"
DEFINE NAMESPACE REACTOME AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/reactome/reactome-20170911.belns"
DEFINE NAMESPACE EFO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/efo/efo-20171206.belns"

DEFINE NAMESPACE UNIPROT AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM AS PATTERN "^\d+$"
DEFINE NAMESPACE dbSNP AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE FIXMECHEM AS LIST {".."}
DEFINE NAMESPACE FIXME AS LIST {"cell damage"}
DEFINE NAMESPACE TAX AS PATTERN "^.*$"
##################################################################################
# ANNOTATIONS Section
##################################################################################
DEFINE ANNOTATION Anatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20190128.belanno"
DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"
DEFINE ANNOTATION CellLine AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20190128.belanno"
DEFINE ANNOTATION Cell AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20190128.belanno"
DEFINE ANNOTATION Disease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20200622.belanno"
DEFINE ANNOTATION MeSHAnatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20190128.belanno"
DEFINE ANNOTATION MeSHDisease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20190128.belanno"
#DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20200622.belanno"
DEFINE ANNOTATION TextLocation AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"
DEFINE ANNOTATION BioAssay AS URL   "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/bao/bao-20190130.belanno"
DEFINE ANNOTATION Gender AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION Technique AS LIST {"fluorescence fusion assay"}
DEFINE ANNOTATION HoursPostInfection AS LIST {"72 hours"}

# Document Annotations #
DEFINE ANNOTATION PublicationType AS LIST {"Review", "Research"}
DEFINE ANNOTATION PublicationStatus AS LIST {"Preprint", "Published"}
DEFINE ANNOTATION Section AS LIST {"Full Text", "Abstract", "Results", "Discussions"}

##################################################################################
# Statements #
##################################################################################
##################################################################################
#example#
SET Citation = {"PubMed", "Sci Adv. 2023 Jun 9;9(23):eadg2248. doi: 10.1126/sciadv.adg2248. Epub 2023 Jun 7.", "37285437"}
SET PublicationType = "Research"
SET PublicationStatus = "Published"
SET Section = "Results"

SET Support = "Seventy-two hours post-infection (hpi), the cultures were fixed and \
examined by confocal fluorescence microscopy, revealing the presence \
of fused neurons [positive for the neuronal marker microtubule- \
associated protein 2 (MAP2)], as characterized by the presence of both \
the GFP and mCherry fluorescent proteins for all the SARS-CoV-2 titers \
used."

SET Species = "10090"
SET Disease = "COVID-19"
SET BioAssay = "fluorescence microscopy"
SET Cell = "neuron"
SET HoursPostInfection = "72 hours"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") increases bp(GO:"induction by virus of host cell-cell fusion")
UNSET BioAssay
UNSET Cell
UNSET HoursPostInfection

SET Support = "Antibody staining revealed that fused neurons were positive for the \
fusogenic spike S protein , which was distributed across the surface \
of infected neurons."

SET BioAssay = "immunohistochemistry"
SET Cell = "neuron"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") increases bp(GO:"induction by virus of host cell-cell fusion")
p(UNIPROT:"P0DTC2") increases bp(GO:"induction by virus of host cell-cell fusion")
UNSET Cell

SET Support = "Upon SARS-CoV-2 infection, glial cells expressing hACE2 were also \
positive for the spike S protein (fig. S1, C and D), and we observed \
additional fusion phenotypes, including neuron-glia (fig. S2, A to C) \
and glia-glia fusion (fig. S2, D to F)."

SET Cell = {"neuron", "glial cell"}
a(TAX:"Severe acute respiratory syndrome coronavirus 2") increases bp(GO:"induction by virus of host cell-cell fusion")
UNSET Cell
UNSET BioAssay
UNSET Species

SET Support = "In agreement with previous findings (2), we found that high doses of \
neuronal SARS-CoV-2 infection resulted in cell damage, a phenotype \
that was not apparent at the lowest doses used within 72 hpi (fig. S1, \
E and F)."

SET Species = "9606"
SET Cell = "neuron"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") increases path(FIXME:"cell damage")
UNSET Cell

SET Support = "Similar to what was observed after infection of 2D neuronal cultures, \
we found that SARS-CoV-2–infected brain organoids exhibited neuronal \
syncytia formed by GFP-interconnected neurons."

SET Cell = "neuron"
SET MeSHAnatomy = {"Brain", "Organoids"}
a(TAX:"Severe acute respiratory syndrome coronavirus 2") increases bp(GO:"induction by virus of host cell-cell fusion")
UNSET MeSHAnatomy

SET Support = "Our results revealed that the expression of p15 was sufficient to \
induce neuronal fusion, as detected by the presence of neurons \
containing both the GFP and mCherry fluorescent proteins a phenotype \
that was never observed when the control vector was cotransfected in \
the absence of p15."

SET Technique = "fluorescence fusion assay"
p(UNIPROT:Q918V6) increases bp(GO:"induction by virus of host cell-cell fusion")

SET Support = "The expression of the fusogen spike S and its receptor hACE2 in \
adjacent cells resulted in the fusion of these neurons and the mixing \
of the fluorescent proteins."

composite(p(UNIPROT:"P0DTC2"), p(HGNC:ACE2)) increases bp(GO:"induction by virus of host cell-cell fusion")
UNSET Cell

SET Support = "With both p15 and spike S, we observed not only neuron-neuron fusion \
but also neuron-glia and glia-glia fusion when the fusogens were \
expressed in these cell types (fig. S4), a result that mimics our \
observation with SARS-CoV-2 infection."

SET Cell = {"neuron", "glial cell"}
p(UNIPROT:Q918V6) increases bp(GO:"induction by virus of host cell-cell fusion")
p(UNIPROT:"P0DTC2") increases bp(GO:"induction by virus of host cell-cell fusion")
UNSET Cell
UNSET Technique

SET Support = "During our analysis of p15 expressed in mechanosensory neurons, we \
observed additional phenotypes that included defects in axonal \
guidance and axonal maintenance (visible as axonal breakages), as well \
as loss of mechanosensory neurons (fig. S6, D and E)."

SET Cell = "neuron"
SET Species = "6239"
p(UNIPROT:Q918V6) decreases bp(GO:"axon guidance")
p(UNIPROT:Q918V6) decreases bp(GO:"neuron projection maintenance")
p(UNIPROT:Q918V6) decreases bp(GO:"mechanosensory behavior")
UNSET Species
UNSET Cell

SET Support = "Similar to what we observed in neuronal cultures and in C. elegans \
mechanosensory neurons, p15 expression resulted in neuronal fusion \
presenting as clusters of interconnected GFP-positive cells (fig. S7, \
A to E, and movie S2). Neuronal fusion was observed both in the \
hippocampus and in the cortex (fig. S7, F to I), representing up to \
15% of transduced hippocampal neurons at 14 days after AAV infection \
(fig. S7H)."

SET Species = "10090"
SET MeSHAnatomy = {"Hippocampus", "Cerebral Cortex"}
SET Cell = "neuron"
p(UNIPROT:Q918V6) increases bp(GO:"induction by virus of host cell-cell fusion")
UNSET MeSHAnatomy

SET Support = "Three days after the expression of the fusogens, the neuronal cultures \
were inspected, revealing the formation of neuronal clusters of \
interconnected GFP-positive cells that resembled the neuronal \
syncytium observed after p15 expression in murine neurons (fig. S8, A \
to C)."

p(UNIPROT:Q918V6) increases bp(GO:"induction by virus of host cell-cell fusion")

SET Support = "We found that, upon SARS-CoV-2 infection, neuronal fusion occurred \
between the somas, as well as between the neurites far from the somas \
(Fig. 1D and fig. S9, A and B)."

a(TAX:"Severe acute respiratory syndrome coronavirus 2") increases bp(GO:"induction by virus of host cell-cell fusion")

UNSET Cell
SET Support = "Our results reveal not only the presence of mCardinal in both fused \
neurons but also the movement of mitochondria between them (Fig. 3, C \
to E, and movie S3)."

bp(GO:"induction by virus of host cell-cell fusion") increases tloc(a(GO:mitochondrion), fromLoc(CELL:neuron), toLoc(CELL:neuron))

SET Support = "Every neuron that fused with glial cells presented a complete loss in \
neuronal activity (fig. S12, E to G)."

SET MeSHAnatomy = {"Brain", "Organoids"}
a(TAX:"Severe acute respiratory syndrome coronavirus 2") increases complex(a(MESH:Neurons),a(MESH:Neuroglia))
complex(a(MESH:Neurons),a(MESH:Neuroglia)) decreases bp(GO:"neuronal signal transduction")
UNSET MeSHAnatomy

SET Support = "The intracellular Ca2+ concentration increased within the bridges \
(fig. S14, C to E), forming Ca2+ peaks that reflected the patterns of \
neuronal activity (fig. S14, F and G)."

SET MeSHAnatomy = {"Brain", "Organoids", "Intracellular Space"}
a(TAX:"Severe acute respiratory syndrome coronavirus 2") increases a(CHEBI:"calcium ion")
UNSET MeSHAnatomy
UNSET Species
UNSET Citation

